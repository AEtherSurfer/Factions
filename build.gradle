ext {
  buildNumber = ''
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'

group = "com.massivecraft"
version = "2.0.3AES-SNAPSHOT"

sourceCompatibility = 1.7

repositories {
	mavenLocal()
    mavenCentral()
	maven {
		url "http://repo.bukkit.org/content/groups/public"
	}
	maven {
		url "http://ci.herocraftonline.com/plugin/repository/everything"
    }
}

dependencies {
	compile 'org.bukkit:bukkit:1.6.2-R0.1-SNAPSHOT'
	compile('com.massivecraft:MCore:6.4.3AES-SNAPSHOT') {
		exclude module: 'ProtocolLib'
		exclude module: 'Privileges'
		exclude module: 'craftconomy3'
		exclude module: 'SDFEconomy'
	}
    compile fileTree(dir: 'lib', include: '*.jar')
}

sourceSets {
	main {
		java {
			srcDir 'src'
		}
		resources {
			srcDir "$projectDir"
			include 'plugin.yml'
		}
	}
}

processResources  {
	filter {
		line ->
		line
		.replace('${name}', project.name)
		.replace('${version}', project.version)
		.replace('${buildNumber}', project.ext.buildNumber)
		.replace('${pluginMainClass}', "${project.group}.${project.name.toLowerCase()}.$project.name")
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '1.6'
}